apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.0"
group = "com.oliveira"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

dependencies {

}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Android Bubble Text Library'
            packaging 'aar'
            description 'Bubble Text library for Android applications'
            url 'https://github.com/ghophp/android-bubble-text'

            scm {
                url 'scm:git@github.com:ghophp/android-bubble-text.git'
                connection 'scm:git@github.com:ghophp/android-bubble-text.git'
                developerConnection 'scm:git@github.com:ghophp/android-bubble-text.git'
            }

            licenses {
                license {
                    name 'MIT'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'holiveira'
                    name 'Guilherme Henrique de Oliveira'
                    email 'ghophp@gmail.com'
                }
            }
        }
    }
}